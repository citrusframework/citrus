[[run-groovy]]
== Groovy

Running Citrus tests in Groovy is a specialization of running Citrus tests in Java.
The Groovy language gives you some syntax sugar and optimized statements that you can use to write your tests.

As usual, you need to choose one of the supported test engines in Citrus to actually run the test (e.g. link:#runtime-junit5[JUnit Jupiter] or link:#runtime-testng[TestNG]).
You can then use the `@CitrusTestSource` annotation to load the Groovy code that represents the test case.

.MyFirstGroovyIT.java
[source,java]
----
package org.citrusframework;

import org.citrusframework.annotations.CitrusTestSource;
import org.citrusframework.testng.TestNGCitrusSupport;
import org.testng.annotations.Test;

public class MyFirstGroovyIT extends TestNGCitrusSpringSupport {

    @Test
    @CitrusTestSource(type = "groovy", name="my-test.groovy")
    public void myFirstGroovyIT() {
    }
}
----

The test logic itself is coded in a Groovy file like this:

.my-test.groovy
[source,groovy]
----
import static org.citrusframework.actions.EchoAction.Builder.echo

name "MyFirstGroovyTest"
author "Christoph"
status "FINAL"
description "Sample test in Groovy"

variables {
    text: "Hello from Citrus!"
}

actions {
    $(echo().message("${text}"))
}
----

[[run-groovy-configuration-dsl]]
=== Configuration scripts

A Groovy test is able to create endpoints and other configuration entities as part of the test.
The script creates these instances as beans in the Citrus registry.

A configuration script may use methods and closures to define the bean instances.
The configuration script supports following methods:

* queues()
* endpoints()
* beans()

.my-test.groovy
[source,groovy]
----
configuration {
    queues {
        queue('say-hello')
    }

    endpoints {
        direct('hello')
            .asynchronous()
            .queue('say-hello')
    }

    beans {
        bean(DefaultTextEqualsMessageValidator.class)
    }
}

actions {
    $(send().endpoint(hello)
            .message()
            .body("Hello Citrus!")
    )
}
----

The test above creates a new `direct` endpoint called `hello` that uses an in-memory queue named `say-hello`.
Also, the configuration script initializes a new bean instance of type `org.citrusframework.validation.DefaultTextEqualsMessageValidator.class`.

The `send` test action is able to reference the new endpoint by its name to send a message body to the queue.
A `receive` test action will automatically resolve the new message validator instance from the Citrus bean registry to perform its message validation.

The Groovy language provides a comfortable way to define common beans in the Citrus registry.

.Create beans in registry
[source,groovy]
----
configuration {
    beans {
        bean('foo', org.example.foo.FooBean.class)
        fooClient = new org.example.foo.FooClientBuilder().build()
    }
}
----

The example creates a new instance of `FooBean` with the id `foo` in the Citrus bean registry.
The `FooBean` class must have a default constructor in this case.

You can also create the instances on your own (e.g. using a builder factory) as seen with the `fooClient` bean in this example.

All beans added to the Citrus registry are subject to being injected into or resolved by test actions and other components in Citrus.
For instance a JMS test action requires a client connection factory bean instance in the Citrus registry.

[[run-groovy-gherkin]]
=== Gherkin style syntax

The Gherkin `Given-When-Then` syntax helps you to categorize and structure the individual steps in your test.
The Groovy test language in Citrus can use this concept, too.

.my-test.groovy
[source,groovy]
----
configuration {
    queues {
        queue('say-hello')
    }

    endpoints {
        direct('hello') {
            asynchronous()
                .queue('say-hello')
        }
    }
}

given:
    $(createVariables()
        .variable("text", "Citrus rocks!")
    )

when:
    $(send().endpoint(hello)
            .message()
            .body('${text}')
    )

then:
    $(receive().endpoint(hello)
            .message()
            .body('${text}')
    )
----

[[run-groovy-message-builder]]
=== Message builders

The Groovy language is good choice when it comes to creating domain specific languages.
You can provide syntactical sugar to make it as comfortable as possible to use your domain.

As an example you can use message builders to create message body payloads in XML or Json format.

.my-test.groovy
[source,groovy]
----
configuration {
    queues {
        queue('say-hello')
    }

    endpoints {
        direct('hello') {
            asynchronous()
                .queue('say-hello')
        }
    }
}

given:
    $(createVariables()
        .variable("text", "Citrus rocks!"))

when:
    $(send().endpoint(hello)
        .message {
            body {
                json()
                    .greeting {
                        text '${text}'
                        language 'eng'
                    }
            }
            headers {
                operation = "sayHello"
            }
        })

then:
    $(receive().endpoint(hello)
        .message {
            body().json {
                greeting {
                    text '${text}'
                    language 'eng'
                }
            }
            headers {
                operation = "sayHello"
            }
        })
----

The example above deals with Json message payloads and leverages the message builders to create a Json body like this:

.body.json
[source,json]
----
{
  "greeting": {
    "text": "${text}",
    "language": "eng"
  }
}
----

The same approach works fine with XML payloads:

.my-test.groovy
[source,groovy]
----
configuration {
    queues {
        queue('say-hello')
    }

    endpoints {
        direct('hello') {
            asynchronous()
                .queue('say-hello')
        }
    }
}

given:
    $(createVariables()
        .variable("text", "Citrus rocks!"))

when:
    $(send().endpoint(hello)
        .message {
            body {
                xml()
                    .greeting(language: 'eng') {
                        text '${text}'
                    }
            }
            headers {
                operation = "sayHello"
            }
        })

then:
    $(receive().endpoint(hello)
        .message {
            body().xml {
                greeting(language: 'eng') {
                    text '${text}'
                }
            }
            headers {
                operation = "sayHello"
            }
        })
----

The test actions use the XML message builder and create XML body payloads such as:

.body.xml
[source,xml]
----
<greeting language='eng'>
  <text>Citrus rocks!</text>
</greeting>
----

[[run-groovy-templates]]
=== Templates

Templates group action sequences to a logical unit.
You can think of templates as reusable components that are used in several tests.
The maintenance is much more efficient because you need to apply changes only on the templates and all referenced use cases are updated automatically.

The template gets identified by a unique name.
Inside a test case we apply the template referencing this unique name.
Have a look at a first example:

.echo-template.groovy
[source,groovy]
----
import static org.citrusframework.actions.EchoAction.Builder.echo

name "echo"
description "Sample template in Groovy"

parameters {
    message="Citrus rocks!"
}

actions {
    $(echo().message('${message}'))
}
----

The code example above describes a template called `echo`.
Templates hold a sequence of test actions or calls other templates themselves.
The template may define a set of parameters that represent customizable user input when applying the template.

The template is available for multiple tests that may apply the template as part of their test action sequence.

.Apply templates
[source,groovy]
----
import static org.citrusframework.container.Template.Builder.applyTemplate

name "ApplyTemplateTest"
author "Christoph"
status "FINAL"
description "Sample test in Groovy"

actions {
    $(applyTemplate().templateName("echo"))

    $(applyTemplate().templateName("echo")
        .parameter("text", "Hello from Citrus!"))
}
----

The example references the `echo` template and call it as part of the test with `applyTemplate()`.
You can customize the template parameter to overwrite the default variable `text` with a custom value.

The example above assumes that you have added the template to the Citrus bean registry with the name `echo`.
If this is not the case you may also reference the file resource path when applying the template:

.Apply templates
[source,groovy]
----
import static org.citrusframework.container.Template.Builder.applyTemplate

actions {
    $(applyTemplate().file("classpath:templates/groovy/echo-template.groovy"))
}
----

[[run-groovy-template-parameters]]
==== Template parameters

How to handle test variables when dealing with templates, in particular when templates are defined in separate source files?
A template may use different variable names compared to those names used a test and vice versa.
The template fails immediately when respective variables are not present.
Therefore, templates introduce the concept of parameters to declare its variable names.

Assume you have the following template:

.sayHello template
[source,groovy]
----
import static org.citrusframework.actions.EchoAction.Builder.echo

name "sayHello"
description "Sample template in Groovy"

parameters {
    user="Citrus"
}

actions {
    $(echo().message('Hello ${user}!'))
}
----

The template `sayHello` in the example uses the variable `${user}`.

In case the test case already uses the template parameter as a value you do not need to set this variable explicitly on the apply template action.

.Implicit template parameter
[source,groovy]
----
import static org.citrusframework.container.Template.Builder.applyTemplate

name "SayHelloTemplateTest"
description "Sample test in Groovy"

variables {
    user = "Christoph"
}

actions {
    $(applyTemplate().templateName("sayHello"))
}
----

.Output
[source,text]
----
Hello Christoph!
----

NOTE: The implicit template parameter may break as soon as the template or the test changes its parameter or variable name.

The test case may also explicitly set the template parameter when applying the template:

.Explicit template parameter
[source,groovy]
----
import static org.citrusframework.container.Template.Builder.applyTemplate

name "SayHelloTemplateTest"
description "Sample test in Groovy"

variables {
    username = "Mickey"
}

actions {
    $(applyTemplate().templateName("sayHello")
        .parameter("user", "${username}"))
}
----

.Output
[source,text]
----
Hello Mickey!
----

The variable *username* is already present in the test case and gets translated into the *user* parameter.
Following from that the template works fine although test and template do work on different variable names.

With template parameters you are able to solve the calibration effort when working with templates and variables.
It is always a good idea to check the used variables/parameters inside a template when calling it.
There might be a variable that is not declared yet inside your test.
So you need to define this value as a parameter.

[[run-groovy-template-paremeter-scope]]
==== Template parameter scope

A template is able to change a test variable value for the rest of the test case.
It is important to know about this when changing test variables in a template as it affects the descendant test actions.
Especially when running test actions or templates in parallel to each other this might lead to unexpected behavior.
You may choose the template parameter scope for this scenario.

IMPORTANT: When a template works on variable values and parameters changes to these variables will automatically affect the
variables in the whole test. So if you change a variable's value inside a template and the variable is defined inside the
test case the changes will affect the variable in a global context. We have to be careful with this when executing a template
several times in a test, especially in combination with parallel containers (see link:#containers-parallel[containers-parallel]).

.print template
[source,groovy]
----
import static org.citrusframework.actions.EchoAction.Builder.echo

name "print"
description "Sample template in Groovy"

parameters {
    index = "0"
    area = "Atlantis"
}

actions {
    $(echo().message('#${index} Hello ${area}!'))
}
----

.Global scope parameter (default)
[source,groovy]
----
actions {
    $(parallel().actions(
        applyTemplate().templateName("print")
            .parameter("index", "1")
            .parameter("area", "Europe"),
        applyTemplate().templateName("print")
            .parameter("index", "2")
            .parameter("area", "Asia"),
        applyTemplate().templateName("print")
            .parameter("index", "3")
            .parameter("area", "Africa")
        )
    )
}
----

In the listing above a template *print* is called several times in a parallel container.
The parameter values will be handled in a global context, so it is quite likely to happen that the template instances influence each other during execution.
We might get such print messages:

.Output
[source,text]
----
#2 Hello Europe!
#2 Hello Africa!
#3 Hello Africa!
----

Index parameters do not fit and the message *'Hello Asia'* is completely gone.
This is because templates overwrite parameters to each other as they are executed in parallel at the same time.
To avoid this behavior we need to tell the template that it should handle parameters as well as variables in a local context.
This will enforce that each template instance is working on a dedicated local context.
See the *globalContext()* method that is set to *false* in this example:

.print template
[source,groovy]
----
import static org.citrusframework.actions.EchoAction.Builder.echo

name "print"
description "Sample template in Groovy"

globalContext(false)

parameters {
    index = "0"
    area = "Atlantis"
}

actions {
    $(echo().message('#${index} Hello ${area}!'))
}
----

After that template instances will not influence each other anymore.
But notice that variable changes inside the template then do not affect the test case neither.
